/**
 * SourceSets are only responsible for compiling source code,
 * but do not deal with executing the byte code (= the tests).
 * We need to create a 'functionalTest(type: Test)' task.
 */
sourceSets {
    integrationTest {
        java {
            srcDirs('src/integrationTest/java')
            resources.srcDirs('src/integrationTest/resources')
        }
        /**
         * Provides, at compile-time, the classes produced by the _main_ and _test_ SourceSets,
         * allowing the integration tests to access the production code in _main_ and allowing
         * them to reuse any unit test helper methods in _test_.
         */
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

tasks.register("writePluginClassPath"){
    def buildDirectory = project.layout.buildDirectory.get()
    def outputDir = file("$buildDirectory/integrationTest/resources")

    inputs.files configurations.compileClasspath + configurations.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/pluginClassPath.txt").text = configurations.compileClasspath.files.join("\n") + "\n" + configurations.runtimeClasspath.files.join("\n")
    }
}

/**
 * Task for executing the integration tests. It references the 
 * classes and runtime classpath of the integration test source set.
 */
task jarTest(type: Test) {
    description = 'Runs integration tests against the installed gretl jar.'
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    useJUnit {
        excludeCategories 'ch.so.agi.gretl.testutil.QgisReachableTest'
        excludeCategories 'ch.so.agi.gretl.testutil.S3Test'
    }

    systemProperty('GRETL_TESTTYPE', 'jar')

    testLogging.showStandardStreams = true

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    jvmArgs '-Xmx1g'
}

task jarS3Test(type: Test, dependsOn: writePluginClassPath) {
    description = 'Runs the S3 integration tests against the installed gretl jar.'
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    useJUnit {
        includeCategories 'ch.so.agi.gretl.testutil.S3Test'
    }

    systemProperty('GRETL_TESTTYPE', 'jar')

    testLogging.showStandardStreams = true

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    jvmArgs '-Xmx1g'
}

task imageTest(type: Test, dependsOn: writePluginClassPath){
    description = 'Runs integration tests against the installed gretl docker image.'
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    
    useJUnit {
        excludeCategories 'ch.so.agi.gretl.testutil.S3Test'
    }
    
    systemProperty('GRETL_TESTTYPE', 'image')

    testLogging.showStandardStreams = true

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
}

task imageS3Test(type: Test, dependsOn: writePluginClassPath){
    description = 'Runs S3 integration tests against the installed gretl docker image.'
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    useJUnit {
        includeCategories 'ch.so.agi.gretl.testutil.S3Test'
    }

    systemProperty('GRETL_TESTTYPE', 'image')

    testLogging.showStandardStreams = true

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
}