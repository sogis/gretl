apply from: '../dependencies.gradle'
apply plugin: 'java-library'

configurations {
    api {
        exclude module: 'xmlparserv2'
        exclude module: 'ucp'
        exclude module: 'xdb6'
        exclude module: 'oraclepki'
        exclude module: 'osdt_core'
        exclude module: 'osdt_cert'
    }  
}

configurations.all {
    
    resolutionStrategy { 
        force 'ch.interlis:iox-ili:1.20.13'
        force 'ch.interlis:ili2c-tool:5.0.2'
        force 'ch.interlis:ili2c-core:5.0.2'
    }
}

dependencies {
    // Add latest gretl jar. Since it's not added as plugin,
    // it will not try to get it from plugins.gradle.org.
    api 'ch.so.agi:gretl:latest.integration'

    // Add 3rd party libs which will want in the docker image.
    api 'de.undercouch:gradle-download-task:3.4.1'
    api 'org.hidetake:gradle-ssh-plugin:2.9.0'
    api 'jp.classmethod.aws:gradle-aws-plugin:0.41'

    // Add ilivalidator custom functions
    api 'ch.so.agi.ilivalidator:ilivalidator-custom-functions:1.1.2'
    
    // Gretl has no oracle dependency: If someone wants to use
    // Gretl but does not have a oracle account, he cannot run
    // Gretl... We copy the jdbc jar into the docker image, our
    // gret-runtime artifact.
    api oracleDependency
}

task stageJars(type: Copy) {
    description = 'Copies all jars required by gretl to the temp folder __jars4image.'
    from configurations.compileClasspath
    from configurations.runtimeClasspath
    into "gretl/__jars4image"
}
